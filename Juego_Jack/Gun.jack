
class Gun {

    field int x, y;           // the Gun's screen location
    field int width, height;  // the Gun's width and height
    field int direction;      // direction of the Gun's movement (1 = left, 2 = right)

    /** Constructs a new Gun with the given location and width. */
    constructor Gun new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 2;
        do show();
        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Shows the Gun. */
    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    /** Hides the Gun. */
    method void hide() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    /** Draws the Gun. */
    method void draw() {
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    /** Sets the Gun's direction (0=stop, 1=left, 2=right). */
    method void setDirection(int Adirection) {
        let direction = Adirection;
        return;
    }

    /** Returns the Gun's left edge. */
    method int getLeft() {
        return x;
    }

    /** Returns the Gun's right edge. */
    method int getRight() {
        return x + width;
    }

    /** Sets the Gun's width. */
    method void setWidth(int Awidth) {
        do hide();
        let width = Awidth;
        do show();
        return;
    }

    /** Moves the Gun one step in the Gun's direction. */
    method void move() {
	    if (direction = 1) {
            let x = x - 4;
            if (x < 20) { let x = 20; }
            do Screen.setColor(false);
            do Screen.drawRectangle((x + width) + 1, y, (x + width) + 4, y + height); // Creo que aquí es el por qué se pone blanca esa cosa
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 3, y + height);
        }
        else {
            let x = x + 4;
            if ((x + width) > 491) { let x = 491 - width; }
            do Screen.setColor(false);
            do Screen.drawRectangle(x - 4, y, x - 1, y + height);
            do Screen.setColor(true);
            do Screen.drawRectangle((x + width) - 3, y, x + width, y + height);
        }
        return;
    }
}
